<!--#include file="connect.asp"-->
<!--#include file="functionOcupacao.asp"-->
<%
lDe = cdate(ref("Data"))
lAte = lDe + 7
rfLocais = "|UNIDADE_ID"& ref("Unidade") &"|"
'response.write(De&", "&Ate&", "&refEspecialidade&", """", """", """", "& rfLocais)    
call ocupacao(lDe, lAte, ref("Especialidades"), "", "", "", rfLocais)    
%>


<table class="table table-condensed table-bordered table-hover">
    <thead>
        <tr>
            <th>Profissional</th>
            <th>Especialidade</th>
            <th>Unidade</th>
            <%
            dataN = lDe
            while dataN<lAte
                %>
                <th><%= ucase(left(weekdayname(weekday(dataN)),3)) %></th>
                <%
                dataN = dataN+1
            wend
            %>
        </tr>
    </thead>
    <tbody>
        <%
        set prof = db.execute("select distinct ro.ProfissionalID, ro.EspecialidadeID, ro.UnidadeID, prof.NomeProfissional, esp.especialidade FROM rel_ocupacao ro LEFT JOIN profissionais prof ON prof.id=ro.ProfissionalID LEFT JOIN especialidades esp ON ro.EspecialidadeID=esp.id WHERE ro.sysUser="& session("User") &" ORDER BY prof.NomeProfissional")
        while not prof.eof
            nomUn = getNomeLocalUnidade(prof("UnidadeID"))&""
            %>
            <tr>
                <td><%= prof("NomeProfissional") %></td>
                <td><%= prof("Especialidade") %></td>
                <td><%= right(nomUn, len(nomUn)-2) %></td>
                <%
                dataN = lDe
                while dataN<lAte
                    set conta = db.execute("select (select count(ro.id) from rel_ocupacao ro WHERE ro.Data="& mydatenull(DataN) &" AND ro.Situacao IN('A','B') AND ro.sysUser="& session("User") &" AND ro.ProfissionalID="& prof("ProfissionalID") &" AND ro.UnidadeID="& treatvalnull(prof("UnidadeID")) &" AND ro.EspecialidadeID="& treatvalnull(prof("EspecialidadeID")) &" AND ro.StaID NOT IN(11,15)) A, (select count(ro.id) from rel_ocupacao ro WHERE ro.Data="& mydatenull(DataN) &" AND ro.Situacao='V' AND ro.sysUser="& session("User") &" AND ro.ProfissionalID="& prof("ProfissionalID") &" AND ro.UnidadeID="& treatvalnull(prof("UnidadeID")) &" AND ro.EspecialidadeID="& treatvalnull(prof("EspecialidadeID")) &") V")
                    Agendados = ccur(conta("A"))
                    Vazios = ccur(conta("V"))

                    perc = 0
                    classe = ""
                    if Vazios+Agendados=0 then
                        classe = "dark"
                    elseif (Vazios+Agendados)>0 and Agendados<=(Vazios+Agendados) then
                        perc = Agendados / (Vazios+Agendados)
                        if perc=0 then
                            classe = "white"
                        elseif perc>0 and perc<=0.25 then
                            classe = "info"
                        elseif perc>0.25 and perc<=0.5 then
                            classe = "system"
                        elseif perc>0.5 and perc<=0.75 then
                            classe = "alert"
                        elseif perc>0.75 and perc<1 then
                            classe = "warning"
                        elseif perc>=1 then
                            classe = "danger"
                        end if

                    end if
                    %>
                    <td>
                        <a class="btn btn-sm btn-block btn-<%= classe %>" href="./?P=agendamultipla&Pers=1&Data=<%= dataN %>&Profissionais=|<%= prof("ProfissionalID") %>|&Locais=|UNIDADE_ID<%= prof("UnidadeID") %>|&Especialidades=|<%= prof("EspecialidadeID") %>|" target="_blank">
                            <%= Agendados &"/"& Vazios+Agendados %> <%'= perc %>
                        </a>
                    </td>
                    <%
                    dataN = dataN+1
                wend
                %>
            </tr>
            <%
        prof.movenext
        wend
        prof.close
        set prof = nothing
        %>
    </tbody>
</table>
